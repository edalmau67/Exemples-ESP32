#define BUTTON_PIN 13   // El botó està connectat a aquest pin
#define DEBOUNCE_TIME 50 // Temps de delay del botó en milisegons

int lastSteadyState = HIGH;       // Estat estable anterior
int lastFlickerableState = HIGH;  // Estat temporal anterior
int currentState;                 // Estat actual

unsigned long lastDebounceTime = 0;  
int counter = 0; // Comptador de pulsacions

void setup() {
  Serial.begin(9600);
  pinMode(BUTTON_PIN, INPUT_PULLUP);
}

// Funció que comprova si el botó ha estat premut
bool buttonPressed() {
  currentState = digitalRead(BUTTON_PIN);

  if (currentState != lastFlickerableState) {
    lastDebounceTime = millis();
    lastFlickerableState = currentState;
  }

  if ((millis() - lastDebounceTime) > DEBOUNCE_TIME) {
    if (lastSteadyState != currentState) {
      lastSteadyState = currentState;

      // Retorna true si el botó està premut (LOW amb INPUT_PULLUP)
      return (currentState == LOW);
    }
  }
  return false;
}

void loop() {
  if (buttonPressed()) {
    counter++; // incrementa el comptador
    Serial.print("Botó premut! Comptador: ");
    Serial.println(counter);
  }
}
